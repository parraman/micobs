/*******************************************************************************
 * Copyright (c) 2013-2015 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     MICOBS SRG Team - Initial API and implementation
 ******************************************************************************/
grammar es.uah.aut.srg.micobs.mesp.lang.CTOOL with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/mesp/mespctool" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://srg.aut.uah.es/micobs/system" as system
import "http://srg.aut.uah.es/micobs/common" as common
import "http://srg.aut.uah.es/micobs/mesp/library/mesplibrary" as mesplibrary

MMESPCTOOLPackageFile returns MMESPCTOOLPackageFile:
	'package' package=[mesplibrary::MMESPPackage|QualifiedName] ';'
	('import' imports+=[common::MCommonPackage|QualifiedName] ';')*
	element=MMESPCTOOLPackageElement
	;

MMESPCTOOLPackageElement returns MMESPCTOOLPackageElement:
	MConstructionTool
;


QualifiedName returns ecore::EString:
	ID ('.' ID)*;

Version returns ecore::EString:
	(INT | ((INT)? ID)) ('.' (INT | ((INT)? ID)))*
	;

VersionedQualifiedName returns ecore::EString:
	QualifiedName '(' Version ')'
	;

VersionedQualifiedReferenceName returns ecore::EString:
	(VersionedQualifiedName '::')? ID ('::' ID)*;

MConstructionTool returns MConstructionTool:
	'construction' 'tool'
	name=ID
	('extends' extends+=[MConstructionTool|VersionedQualifiedName] ("," extends+=[MConstructionTool|VersionedQualifiedName])* )?
	'{'
		(('version' ':=' version=Version ';')
		& ('supported' 'languages' ':=' supportedLanguages+=[system::MLanguage|VersionedQualifiedName] ("," supportedLanguages+=[system::MLanguage|VersionedQualifiedName])* ';'))
    '}'
    ';';