/*******************************************************************************
 * Copyright (c) 2013-2015 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     MICOBS SRG Team - Initial API and implementation
 ******************************************************************************/
grammar es.uah.aut.srg.micobs.mesp.lang.PSWP with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/mesp/mesppswp" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://srg.aut.uah.es/micobs/common" as common
import "http://srg.aut.uah.es/micobs/mesp/mesposswp" as mesposswp
import "http://srg.aut.uah.es/micobs/pdl" as pdl
import "http://srg.aut.uah.es/micobs/mesp/mespctool" as mespctool
import "http://srg.aut.uah.es/micobs/mesp/library/mesplibrary" as mesplibrary

MMESPPSWPPackageFile returns MMESPPSWPPackageFile:
	'package' package=[mesplibrary::MMESPPackage|QualifiedName] ';'
	('import' imports+=[common::MCommonPackage|QualifiedName] ';')*
	element=MMESPPSWPPackageElement
    ;

MMESPPSWPPackageElement returns MMESPPSWPPackageElement:
	MPlatformSwPackage;

QualifiedName returns ecore::EString:
	ID ('.' ID)*;

Version returns ecore::EString:
	(INT | ((INT)? ID)) ('.' (INT | ((INT)? ID)))*
	;

VersionedQualifiedName returns ecore::EString:
	QualifiedName '(' Version ')'
	;

VersionedQualifiedReferenceName returns ecore::EString:
	(VersionedQualifiedName '::')? ID ('::' ID)*;

MPlatformSwPackage returns MPlatformSwPackage:
	'pswpackage'
	name=ID
	'{'
		(('version' ':=' version=Version ';')
		& ('supported platform' ':=' referencedElement=[pdl::MPlatform|VersionedQualifiedName] ';')
		& ('construction tools' ':=' ctools+=[mespctool::MConstructionTool|VersionedQualifiedName] ("," ctools+=[mespctool::MConstructionTool|VersionedQualifiedName])* ';')
		& ('osswpackages' ':=' osSwPackages+=[mesposswp::MOSSwPackage|VersionedQualifiedName] ("," osSwPackages+=[mesposswp::MOSSwPackage|VersionedQualifiedName])* ';'))
    '}' ';';

