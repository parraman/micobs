/*******************************************************************************
 * Copyright (c) 2013-2015 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     MICOBS SRG Team - Initial API and implementation
 ******************************************************************************/
grammar es.uah.aut.srg.micobs.mclev.domain.edroom.lang.PROT with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/mclev/domain/edroom/edroomprot" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://srg.aut.uah.es/micobs/common" as common
import "http://srg.aut.uah.es/micobs/mclev/mcleviface" as mcleviface
import "http://srg.aut.uah.es/micobs/mclev/domain/edroom/library/edroomlibrary" as edroomlibrary
import "http://srg.aut.uah.es/micobs/mclev/domain/edroom/edroomdclass" as edroomdclass

EDROOMPROTPackageFile returns EDROOMPROTPackageFile:
	'package' package=[edroomlibrary::EDROOMPackage|QualifiedName] ';'
	('import' imports+=[common::MCommonPackage|QualifiedName] ';')*
	element=EDROOMPROTPackageElement
    ;

QualifiedName returns ecore::EString:
	ID ('.' ID)*;

Version returns ecore::EString:
	(INT | ((INT)? ID)) ('.' (INT | ((INT)? ID)))*
	;

VersionedQualifiedName returns ecore::EString:
	QualifiedName '(' Version ')'
	;

VersionedQualifiedReferenceName returns ecore::EString:
	(VersionedQualifiedName '::')? ID ('::' ID)*;

EDROOMPROTPackageElement returns EDROOMPROTPackageElement:
	EDROOMProtocol;

EDROOMProtocol returns EDROOMProtocol:
	{EDROOMProtocol}
	'protocol'
	name=ID
	('extends' extends+=[EDROOMProtocol|VersionedQualifiedName] ( "," extends+=[EDROOMProtocol|VersionedQualifiedName])* )?
	'{'
		(('version' ':=' version=Version ';')
		& ('interface' ':=' referencedElement=[mcleviface::MInterface|VersionedQualifiedName] ';')
		& ('input' 'messages'
		'{'
			(inputMessages+=EDROOMMessage)+
		'}' ';')?
		& ('output' 'messages'
		'{'
			(outputMessages+=EDROOMMessage)+
		'}' ';')?)
	'}'
	';'
	;

EDROOMMessage returns EDROOMMessage:
	EDROOMSendMessage |
	EDROOMInvokeMessage |
	EDROOMReplyMessage;

EDROOMSendMessage returns EDROOMSendMessage:
	{EDROOMSendMessage}
	'send'
	name=ID
	'('
	(dataClass=[edroomdclass::EDROOMDataClass|VersionedQualifiedName])?
	')'
	';'
	;

EDROOMInvokeMessage returns EDROOMInvokeMessage:
	{EDROOMInvokeMessage}
	'invoke'
	name=ID
	'('
	(dataClass=[edroomdclass::EDROOMDataClass|VersionedQualifiedName])?
	')'
	'replies' replies+=[EDROOMReplyMessage|VersionedQualifiedReferenceName] ("," replies+=[EDROOMReplyMessage|VersionedQualifiedReferenceName])*
	';'
	;

EDROOMReplyMessage returns EDROOMReplyMessage:
	{EDROOMReplyMessage}
	'reply'
	name=ID
	'('
	(dataClass=[edroomdclass::EDROOMDataClass|VersionedQualifiedName])?
	')'
	';'
	;
