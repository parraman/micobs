/*******************************************************************************
 * Copyright (c) 2013-2015 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     MICOBS SRG Team - Initial API and implementation
 ******************************************************************************/
grammar es.uah.aut.srg.micobs.mclev.domain.edroom.lang.DCLASS with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/mclev/domain/edroom/edroomdclass" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://srg.aut.uah.es/micobs/common" as common
import "http://srg.aut.uah.es/micobs/mclev/domain/edroom/library/edroomlibrary" as edroomlibrary

EDROOMDCLASSPackageFile returns EDROOMDCLASSPackageFile:
	'package' package=[edroomlibrary::EDROOMPackage|QualifiedName] ';'
	('import' imports+=[edroomlibrary::EDROOMPackage|QualifiedName] ';')*
	element=EDROOMDCLASSPackageElement
    ;

EInt returns ecore::EInt:
	INT;

QualifiedName returns ecore::EString:
	ID ('.' ID)*;

Version returns ecore::EString:
	(INT | ((INT)? ID)) ('.' (INT | ((INT)? ID)))*
	;

VersionedQualifiedName returns ecore::EString:
	QualifiedName '(' Version ')'
	;

VersionedQualifiedReferenceName returns ecore::EString:
	(VersionedQualifiedName '::')? ID ('::' ID)*;

EDROOMDCLASSPackageElement returns EDROOMDCLASSPackageElement:
	EDROOMDataClass
;

EDROOMDataClass returns EDROOMDataClass:
	{EDROOMDataClass}
	'dataclass'
	name=ID
	('inherits' inherits+=[EDROOMDataClass|VersionedQualifiedName] ( "," inherits+=[EDROOMDataClass|VersionedQualifiedName])* )?
	'{'
		(('version' ':=' version=Version ';')
		& (definitions+=EDROOMDataDefinition |
		fields+=EDROOMDataField)*)
	'}'
	';'
;

EDROOMDataDefinition returns EDROOMDataDefinition:
	EDROOMDataDefinitionUnion |
	EDROOMDataDefinitionEnum
;

EDROOMDataDefinitionUnion returns EDROOMDataDefinitionUnion:
	{EDROOMDataDefinitionUnion}
	'union'
	name=ID
	'{'
		elements+=EDROOMDataDefinitionUnionElement
		elements+=EDROOMDataDefinitionUnionElement
		(elements+=EDROOMDataDefinitionUnionElement)*
	'}' ';'	
;

EDROOMDataDefinitionEnum returns EDROOMDataDefinitionEnum:
	{EDROOMDataDefinitionEnum}
	'enum'
	name=ID
	'{'
		elements+=EDROOMDataDefinitionEnumElement ("," elements+=EDROOMDataDefinitionEnumElement)*
	'}' ';'	
;

EDROOMDataDefinitionEnumElement returns EDROOMDataDefinitionEnumElement:
	{EDROOMDataDefinitionEnumElement}
	name=ID
	('=' value=EInt)?
    ;

EDROOMDataDefinitionUnionElement returns EDROOMDataDefinitionUnionElement:
	EDROOMDataDefinitionUnionElementBasic |
	EDROOMDataDefinitionUnionElementBasicArray |
	EDROOMDataDefinitionUnionElementClass |
	EDROOMDataDefinitionUnionElementClassArray |
	EDROOMDataDefinitionUnionElementUnion |
	EDROOMDataDefinitionUnionElementUnionArray |
	EDROOMDataDefinitionUnionElementEnum |
	EDROOMDataDefinitionUnionElementEnumArray
;

EDROOMDataDefinitionUnionElementBasic returns EDROOMDataDefinitionUnionElementBasic:
	{EDROOMDataDefinitionUnionElementBasic}
	type=EDROOMBasicType
	name = ID
	';';

EDROOMDataDefinitionUnionElementBasicArray returns EDROOMDataDefinitionUnionElementBasicArray:
	{EDROOMDataDefinitionUnionElementBasicArray}
	type=EDROOMBasicType
	name = ID
	'['
	elements=EInt
    ']'
	';';

EDROOMDataDefinitionUnionElementClass returns EDROOMDataDefinitionUnionElementClass:
	{EDROOMDataDefinitionUnionElementClass}
	type=[EDROOMDataClass|ID]
	name = ID
	';';

EDROOMDataDefinitionUnionElementClassArray returns EDROOMDataDefinitionUnionElementClassArray:
	{EDROOMDataDefinitionUnionElementClassArray}
	type=[EDROOMDataClass|ID]
	name = ID
	'['
	elements=EInt
    ']'
	';';

EDROOMDataDefinitionUnionElementUnion returns EDROOMDataDefinitionUnionElementUnion:
	{EDROOMDataDefinitionUnionElementUnion}
	'union'
	type=[EDROOMDataDefinitionUnion|VersionedQualifiedReferenceName]
	name = ID
	';';

EDROOMDataDefinitionUnionElementUnionArray returns EDROOMDataDefinitionUnionElementUnionArray:
	{EDROOMDataDefinitionUnionElementUnionArray}
	'union'
	type=[EDROOMDataDefinitionUnion|VersionedQualifiedReferenceName]
	name = ID
	'['
	elements=EInt
    ']'
	';';

EDROOMDataDefinitionUnionElementEnum returns EDROOMDataDefinitionUnionElementEnum:
	{EDROOMDataDefinitionUnionElementEnum}
	'union'
	type=[EDROOMDataDefinitionEnum|VersionedQualifiedReferenceName]
	name = ID
	';';

EDROOMDataDefinitionUnionElementEnumArray returns EDROOMDataDefinitionUnionElementEnumArray:
	{EDROOMDataDefinitionUnionElementEnumArray}
	'union'
	type=[EDROOMDataDefinitionEnum|VersionedQualifiedReferenceName]
	name = ID
	'['
	elements=EInt
    ']'
	';';

EDROOMDataField returns EDROOMDataField:
	EDROOMDataFieldBasic |
	EDROOMDataFieldBasicArray |
	EDROOMDataFieldClass |
	EDROOMDataFieldClassArray |
	EDROOMDataFieldUnion |
	EDROOMDataFieldUnionArray |
	EDROOMDataFieldEnum |
	EDROOMDataFieldEnumArray
;

EDROOMDataFieldBasic returns EDROOMDataFieldBasic:
	{EDROOMDataFieldBasic}
	type=EDROOMBasicType
	name=ID
	';';

EDROOMDataFieldBasicArray returns EDROOMDataFieldBasicArray:
	{EDROOMDataFieldBasicArray}
	type=EDROOMBasicType
	name=ID
	'['
		elements=EInt
    ']'
    ';';

EDROOMDataFieldClass returns EDROOMDataFieldClass:
	{EDROOMDataFieldClass}
	type=[EDROOMDataClass|VersionedQualifiedName]
	name=ID
	';';

EDROOMDataFieldClassArray returns EDROOMDataFieldClassArray:
	{EDROOMDataFieldClassArray}
	type=[EDROOMDataClass|VersionedQualifiedName]
	name=ID
	'['
		elements=EInt
    ']'
    ';';

EDROOMDataFieldUnion returns EDROOMDataFieldUnion:
	{EDROOMDataFieldUnion}
	'union'
	type=[EDROOMDataDefinitionUnion|VersionedQualifiedReferenceName]
	name=ID
    ';';

EDROOMDataFieldUnionArray returns EDROOMDataFieldUnionArray:
	{EDROOMDataFieldUnionArray}
	'union'
	type=[EDROOMDataDefinitionUnion|VersionedQualifiedReferenceName]
	name=ID
	'['
	elements=EInt
    ']'
    ';';

EDROOMDataFieldEnum returns EDROOMDataFieldEnum:
	{EDROOMDataFieldEnum}
	'enum'
	type=[EDROOMDataDefinitionEnum|VersionedQualifiedReferenceName]
	name=ID
    ';';

EDROOMDataFieldEnumArray returns EDROOMDataFieldEnumArray:
	{EDROOMDataFieldEnumArray}
	'enum'
	type=[EDROOMDataDefinitionEnum|VersionedQualifiedReferenceName]
	name=ID
	'['
	elements=EInt
    ']'
    ';';

enum EDROOMBasicType returns EDROOMBasicType:
	TEDROOMBool = 'TEDROOMBool' | TEDROOMByte = 'TEDROOMByte' | 
	TEDROOMDouble = 'TEDROOMDouble' | TEDROOMFloat = 'TEDROOMFloat' | 
	TEDROOMInt16 = 'TEDROOMInt16' | TEDROOMInt32 = 'TEDROOMInt32' | 
	TEDROOMInt64 = 'TEDROOMInt64' | TEDROOMInt8 = 'TEDROOMInt8' | 
	TEDROOMUInt16 = 'TEDROOMUInt16' | TEDROOMUInt32 = 'TEDROOMUInt32' | 
	TEDROOMUInt64 = 'TEDROOMUInt64' | TEDROOMUInt8 = 'TEDROOMUInt8' |
	TEDROOMWord16 = 'TEDROOMWord16' | TEDROOMWord32 = 'TEDROOMWord32' |
	TEDROOMWord64 = 'TEDROOMWord64';