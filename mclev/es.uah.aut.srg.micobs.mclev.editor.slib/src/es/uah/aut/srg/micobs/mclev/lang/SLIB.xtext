/*******************************************************************************
 * Copyright (c) 2013-2015 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     MICOBS SRG Team - Initial API and implementation
 ******************************************************************************/
grammar es.uah.aut.srg.micobs.mclev.lang.SLIB with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/mclev/mclevslib" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://srg.aut.uah.es/micobs/mclev/mclevsai" as mclevsai
import "http://srg.aut.uah.es/micobs/system" as system
import "http://srg.aut.uah.es/micobs/pdl" as pdl
import "http://srg.aut.uah.es/micobs/common" as common
import "http://srg.aut.uah.es/micobs/mclev/library/mclevlibrary" as mclevlibrary

MMCLEVSLIBPackageFile returns MMCLEVSLIBPackageFile:
	'package' package=[mclevlibrary::MMCLEVPackage|QualifiedName] ';'
	('import' imports+=[common::MCommonPackage|QualifiedName] ';')*
	element=MMCLEVSLIBPackageElement
    ;

MMCLEVSLIBPackageElement returns MMCLEVSLIBPackageElement:
	MDriverServiceLibrary | MServiceLibrary | MAbstractServiceLibrary;

QualifiedName returns ecore::EString:
	ID ('.' ID)*;

Version returns ecore::EString:
	(INT | ((INT)? ID)) ('.' (INT | ((INT)? ID)))*
	;

VersionedQualifiedName returns ecore::EString:
	QualifiedName '(' Version ')'
	;

VersionedQualifiedReferenceName returns ecore::EString:
	(VersionedQualifiedName '::')? ID ('::' ID)*;


MAbstractServiceLibrary returns MAbstractServiceLibrary:
	'abstract' 'service' 'library'
	name=ID
	('inherits' inherits+=[MAbstractServiceLibrary|VersionedQualifiedName] ( "," inherits+=[MAbstractServiceLibrary|VersionedQualifiedName])* )?
	'{'
		(('version' ':=' version=Version ';')
		& ('attributes' 
		'{' 
			(attributes+=MParameterWithoutSLSP)+
		'}' ';')?
		& ('provided' 'sais' '{'
			(provides+=MServiceLibraryProvidedSAI)+
		'}' ';')?)
    '}' ';';

MServiceLibraryProvidedSAI returns MServiceLibraryProvidedSAI:
	'provides' 
	sai=[mclevsai::MSAI|VersionedQualifiedName]
	'{'
		(attributeValueAssignments+=MSLibProvidedSAIAVA)*
	'}' ';'
;

MSLibProvidedSAIAVA returns MSLibProvidedSAIAVA:
	MSLibProvidedSAIAVAExpression |
	MSLibProvidedSAIAVASwitch
;

MSLibProvidedSAIAVAExpression returns MSLibProvidedSAIAVAExpression:
	'attribute'
	parameter=[common::MParameter|VersionedQualifiedReferenceName]
	':='
	attributeValue=MParameterValueExpression ';'
;

MSLibProvidedSAIAVASwitch returns MSLibProvidedSAIAVASwitch:
	'attribute' 
	parameter=[common::MParameter|VersionedQualifiedReferenceName]
	':='
	'switch'
	'{'
		(cases+=MSLibProvidedSAIAVASwitchCase)+
	'}' ';'
;

MSLibProvidedSAIAVASwitchCase returns MSLibProvidedSAIAVASwitchCase:
	'case' platform=[MServiceLibrarySupportedPlatform|ID] ':'
	attributeValue=MParameterValueExpression ';'
;

MServiceLibrary returns MServiceLibrary:
	'service' 'library'
	name=ID
	('inherits' inherits+=[MAbstractServiceLibrary|VersionedQualifiedName] ( "," inherits+=[MAbstractServiceLibrary|VersionedQualifiedName])* )?
	('extends' extends+=[MServiceLibrary|VersionedQualifiedName] ( "," extends+=[MServiceLibrary|VersionedQualifiedName])* )?
	'{'
		(('version' ':=' version=Version ';')
		& ('languages' ':=' languages+=[system::MLanguage|VersionedQualifiedName] ("," languages+=[system::MLanguage|VersionedQualifiedName])* ';')
		& ('attributes' 
		'{' 
			(attributes+=MParameter)+
		'}' ';')?
		& ('provided' 'sais' '{'
				(provides+=MServiceLibraryProvidedSAI)+
			'}' ';')?
		& ('requires' ':=' requires+=[mclevsai::MSAI|VersionedQualifiedName] ( "," requires+=[mclevsai::MSAI|VersionedQualifiedName])* ';' )?
		& ('supported' 'platforms' 
		'{' 
			(supportedPlatforms+=MServiceLibrarySupportedPlatform)+	
		'}' ';'))
    '}' ';';

MDriverServiceLibrary returns MDriverServiceLibrary:
	'driver' 'service' 'library'
	name=ID
	('inherits' inherits+=[MAbstractServiceLibrary|VersionedQualifiedName] ( "," inherits+=[MAbstractServiceLibrary|VersionedQualifiedName])* )?
	('extends' extends+=[MServiceLibrary|VersionedQualifiedName] ( "," extends+=[MServiceLibrary|VersionedQualifiedName])* )?
	'{'
		(('version' ':=' version=Version ';')
		& ('languages' ':=' languages+=[system::MLanguage|VersionedQualifiedName] ("," languages+=[system::MLanguage|VersionedQualifiedName])* ';')
		& ('attributes' 
		'{' 
			(attributes+=MParameter)+
		'}' ';')?
		& ('provided' 'sais' '{'
				(provides+=MServiceLibraryProvidedSAI)+
			'}' ';')?
		& ('requires' ':=' requires+=[mclevsai::MSAI|VersionedQualifiedName] ( "," requires+=[mclevsai::MSAI|VersionedQualifiedName])* ';' )?
		& ('supported' 'platforms' 
		'{' 
			(supportedPlatforms+=MDriverSLibSupportedPlatform)+	
		'}' ';'))
    '}' ';';


MServiceLibrarySupportedPlatform returns MServiceLibrarySupportedPlatform:
	{MServiceLibrarySupportedPlatform}
	'supported' 'platform'
	name=ID
	'{'
		(('osapi' ':=' ('any' | osapi=[pdl::MOperatingSystemAPI|VersionedQualifiedName]) ';')
		& ('os' ':=' ('any' | os=[pdl::MOperatingSystem|VersionedQualifiedName]) ';')
		& ('architecture' ':=' ('any' | architecture=[pdl::MArchitecture|VersionedQualifiedName]) ';')
		& ('compiler' ':=' compiler=[pdl::MCompiler|VersionedQualifiedName] ';')?
		& ('microprocessor' ':=' ('any' | microprocessor=[pdl::MMicroprocessor|VersionedQualifiedName]) ';')
		& ('board' ':=' ('any' | board=[pdl::MBoard|VersionedQualifiedName]) ';')
		& ('attributes' 
		'{' 
			(attributes+=MParameterWithoutSLSP)+
		'}' ';' )?
		& ('languages' ':=' languages+=[system::MLanguage|VersionedQualifiedName] ("," languages+=[system::MLanguage|VersionedQualifiedName])* ';')?
		& ('requires' ':=' requires+=[mclevsai::MSAI|VersionedQualifiedName] ( "," requires+=[mclevsai::MSAI|VersionedQualifiedName])* ';' )?)
    '}' ';';

MDriverSLibSupportedPlatform returns MDriverSLibSupportedPlatform:
	{MDriverSLibSupportedPlatform}
	'supported' 'platform'
	name=ID
	'{'
		(('osapi' ':=' ('any' | osapi=[pdl::MOperatingSystemAPI|VersionedQualifiedName]) ';')
		& ('os' ':=' ('any' | os=[pdl::MOperatingSystem|VersionedQualifiedName]) ';')
		& ('architecture' ':=' ('any' | architecture=[pdl::MArchitecture|VersionedQualifiedName]) ';')
		& ('compiler' ':=' compiler=[pdl::MCompiler|VersionedQualifiedName] ';')?
		& ('microprocessor' ':=' ('any' | microprocessor=[pdl::MMicroprocessor|VersionedQualifiedName]) ';')
		& ('board' ':=' ('any' | board=[pdl::MBoard|VersionedQualifiedName]) ';')
		& ('attributes' 
		'{' 
			(attributes+=MParameterWithoutSLSP)+
		'}' ';' )?
		& ('supported' 'devices' '{'
			(supportedDevices+=MDriverSLibSupportedDevice)+
		'}' ';')?
		& ('languages' ':=' languages+=[system::MLanguage|VersionedQualifiedName] ("," languages+=[system::MLanguage|VersionedQualifiedName])* ';')?
		& ('requires' ':=' requires+=[mclevsai::MSAI|VersionedQualifiedName] ( "," requires+=[mclevsai::MSAI|VersionedQualifiedName])* ';' )?)
    '}' ';';

MDriverSLibSupportedDevice returns MDriverSLibSupportedDevice:
	'device' device=[pdl::MDevice|VersionedQualifiedName]
	name=ID '{'
		('required' ':=' required=MParameterValueExpression ';')?
	'}' ';'
;

EBoolean returns ecore::EBoolean:
	'true' | 'false';

terminal HEXADECIMAL returns ecore::EString:
	('0x' (('0'..'9') | ('a'..'f') | ('A'..'F'))+);

INTEGER returns ecore::EString:
	('-'? INT | HEXADECIMAL);

REAL returns ecore::EString:
	'-'? (INT '.' INT) ('e' ('-')? INT)?;

MParameter returns common::MParameter:
	MIntegerParameterSingleExpression | MEnumParameterSingleExpression | 
	MBooleanParameterSingleExpression | MRealParameterSingleExpression |
	MStringParameterSingleExpression | MEnumParameterDefinition |
	MBooleanParamSLSPSwitch | MStringParamSLSPSwitch |
	MIntegerParamSLSPSwitch | MRealParamSLSPSwitch |
	MEnumParamSLSPSwitch;


MParameterWithoutSLSP returns common::MParameter:
	MIntegerParameterSingleExpression | MEnumParameterSingleExpression | 
	MBooleanParameterSingleExpression | MRealParameterSingleExpression |
	MStringParameterSingleExpression | MEnumParameterDefinition;

MBooleanParamSLSPSwitch returns MBooleanParamSLSPSwitch:
	(constant?='const')?
	'boolean' name=ID ':='
	'switch'
	'{'
		(cases+=MBooleanParamSLSPSwitchCase)+
	'}' ';'
;

MBooleanParamSLSPSwitchCase returns MBooleanParamSLSPSwitchCase:
	'case' platform=[MServiceLibrarySupportedPlatform|ID] ':'
	defaultValue=MParameterValueExpression ';'
;

MStringParamSLSPSwitch returns MStringParamSLSPSwitch:
	(constant?='const')?
	'string' name=ID ':='
	'switch'
	'{'
		(cases+=MStringParamSLSPSwitchCase)+
	'}' ';'
;

MStringParamSLSPSwitchCase returns MStringParamSLSPSwitchCase:
	'case' platform=[MServiceLibrarySupportedPlatform|ID] ':'
	defaultValue=MParameterValueExpression ';'
;

MIntegerParamSLSPSwitch returns MIntegerParamSLSPSwitch:
	(constant?='const')?
	'integer' name=ID ':='
	'switch'
	'{'
		(cases+=MIntegerParamSLSPSwitchCase)+
	'}' ';'
;

MIntegerParamSLSPSwitchCase returns MIntegerParamSLSPSwitchCase:
	'case' platform=[MServiceLibrarySupportedPlatform|ID] ':'
	defaultValue=MParameterValueExpression
	('range' range=MParameterRange)? ';'
;

MRealParamSLSPSwitch returns MRealParamSLSPSwitch:
	(constant?='const')?
	'real' name=ID ':='
	'switch'
	'{'
		(cases+=MRealParamSLSPSwitchCase)+
	'}' ';'
;

MRealParamSLSPSwitchCase returns MRealParamSLSPSwitchCase:
	'case' platform=[MServiceLibrarySupportedPlatform|ID] ':'
	defaultValue=MParameterValueExpression
	('range' range=MParameterRange)? ';'
;

MEnumParamSLSPSwitch returns MEnumParamSLSPSwitch:
	(constant?='const')?
	'enum'
	enumDefinition=[common::MEnumParameterDefinition|VersionedQualifiedReferenceName]
	name=ID ':='
	'switch'
	'{'
		(cases+=MEnumParamSLSPSwitchCase)+
	'}' ';'
	;

MEnumParamSLSPSwitchCase returns MEnumParamSLSPSwitchCase:
	'case' platform=[MServiceLibrarySupportedPlatform|ID] ':'
	defaultValue=MParameterValueExpression ';'
;

MIntegerParameterSingleExpression returns common::MIntegerParameterSingleExpression:
	(constant?='const')?
	'integer'
	name=ID
	':=' defaultValue=MParameterValueExpression
	('range' range=MParameterRange)?
    ';';

MEnumParameterDefinition returns common::MEnumParameterDefinition:
	{common::MEnumParameterDefinition}
	'enum'
	name=ID
	'{'
		literals+=MEnumParameterLiteralRule ("," literals+=MEnumParameterLiteralRule)*
    '}'
    ';';

MEnumParameterSingleExpression returns common::MEnumParameterSingleExpression:
	(constant?='const')?
	'enum'
	enumDefinition=[common::MEnumParameterDefinition|VersionedQualifiedReferenceName]
	name=ID
	':=' defaultValue=MParameterValueExpression
    ';';

MRealParameterSingleExpression returns common::MRealParameterSingleExpression:
	(constant?='const')?
	'real'
	name=ID
	':=' defaultValue=MParameterValueExpression
	('range' range=MParameterRange)?
    ';';

MEnumParameterLiteralRule returns common::MEnumParameterLiteral:
	MEnumParameterLiteral |
	MEnumParamIntegerLiteral |
	MEnumParamRealLiteral |
	MEnumParamStringLiteral
	;

MParameterValue returns common::MParameterValue:
	MParameterValueLiteral | MParameterValueRefObject |
	MParameterValuePAR
	;

MParameterValueLiteral returns common::MParameterValueLiteral:
	MParameterValueBooleanLiteral | MParameterValueStringLiteral |
	MParameterValueIntegerLiteral | MParameterValueRealLiteral
;

MParameterValueBooleanLiteral returns common::MParameterValueBooleanLiteral:
	{common::MParameterValueBooleanLiteral}
	value=EBoolean;

MParameterValueStringLiteral returns common::MParameterValueStringLiteral:
	{common::MParameterValueStringLiteral}
	(isRaw?='raw')?
	value=STRING;

MParameterValueIntegerLiteral returns common::MParameterValueIntegerLiteral:
	{common::MParameterValueIntegerLiteral}
	value=INTEGER;

MParameterValueRealLiteral returns common::MParameterValueRealLiteral:
	{common::MParameterValueRealLiteral}
	value=REAL;


MParameterValueRefObject returns common::MParameterValueRefObject:
	{common::MParameterValueRefObject}
	object=[common::MCommonReferenceableObj|VersionedQualifiedReferenceName]
	;
	
MParameterValuePAR returns common::MParameterValuePAR:
	'(' value=MParameterValueExpression ')'
	;

enum MParameterValueExpressionOperators returns common::MParameterValueExpressionOperators:
	plus = '+' | minus = '-';

enum MParameterValueTERMOperators returns common::MParameterValueTERMOperators:
	mult = '*' | div = '/';
	
MParameterValueExpression returns common::MParameterValueExpression:
	{common::MParameterValueExpression}
	left=MParameterValueTERM (operation=MParameterValueExpressionOperators right=MParameterValueExpression)?
	;	

MParameterValueTERM returns common::MParameterValueTERM:
	{common::MParameterValueTERM}
	left=MParameterValue (operation=MParameterValueTERMOperators right=MParameterValueTERM)?
	;

MParameterRange returns common::MParameterRange:
	MParameterOCR | MParameterOOR |
	MParameterCOR | MParameterCCR;

MParameterOCR returns common::MParameterOCR:
	{common::MParameterOCR}
	'('
	lowerValue=MParameterValueExpression ','
	upperValue=MParameterValueExpression
	']'
	;

MParameterOOR returns common::MParameterOOR:
	{common::MParameterOOR}
	'('
	lowerValue=MParameterValueExpression ','
	upperValue=MParameterValueExpression
	')'
	;

MParameterCOR returns common::MParameterCOR:
	{common::MParameterCOR}
	'['
	lowerValue=MParameterValueExpression ','
	upperValue=MParameterValueExpression
	')'
	;

MParameterCCR returns common::MParameterCCR:
	{common::MParameterCCR}
	'['
	lowerValue=MParameterValueExpression ','
	upperValue=MParameterValueExpression
	']'
	;

MEnumParameterLiteral returns common::MEnumParameterLiteral:
	{common::MEnumParameterLiteral}
	name=ID
;

MEnumParamIntegerLiteral returns common::MEnumParamIntegerLiteral:
	{common::MEnumParamIntegerLiteral}
	name=ID
	'='
	value=INTEGER
;

MEnumParamRealLiteral returns common::MEnumParamRealLiteral:
	{common::MEnumParamRealLiteral}
	name=ID
	'='
	value=REAL
;

MEnumParamStringLiteral returns common::MEnumParamStringLiteral:
	{common::MEnumParamStringLiteral}
	name=ID
	'='
	(isRaw?='raw')?
	value=STRING
;

MBooleanParameterSingleExpression returns common::MBooleanParameterSingleExpression:
	(constant?='const')?
	'boolean'
	name=ID
	':=' defaultValue=MParameterValueExpression
	';';

MStringParameterSingleExpression returns common::MStringParameterSingleExpression:
	(constant?='const')?
	'string'
	name=ID
	':=' defaultValue=MParameterValueExpression
	';';
    