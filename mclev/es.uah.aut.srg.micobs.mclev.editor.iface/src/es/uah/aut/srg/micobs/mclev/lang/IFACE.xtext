/*******************************************************************************
 * Copyright (c) 2013-2015 UAH Space Research Group.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     MICOBS SRG Team - Initial API and implementation
 ******************************************************************************/
grammar es.uah.aut.srg.micobs.mclev.lang.IFACE with org.eclipse.xtext.common.Terminals

import "http://srg.aut.uah.es/micobs/mclev/mcleviface" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://srg.aut.uah.es/micobs/mclev/mclevdom" as mclevdom
import "http://srg.aut.uah.es/micobs/common" as common
import "http://srg.aut.uah.es/micobs/mclev/library/mclevlibrary" as mclevlibrary

MMCLEVIFACEPackageFile returns MMCLEVIFACEPackageFile:
	'package' package=[mclevlibrary::MMCLEVPackage|QualifiedName] ';'
	('import' imports+=[common::MCommonPackage|QualifiedName] ';')*
	element=MMCLEVIFACEPackageElement
    ;

MMCLEVIFACEPackageElement returns MMCLEVIFACEPackageElement:
	MInterface;
	
QualifiedName returns ecore::EString:
	ID ('.' ID)*;

Version returns ecore::EString:
	(INT | ((INT)? ID)) ('.' (INT | ((INT)? ID)))*
	;

VersionedQualifiedName returns ecore::EString:
	QualifiedName '(' Version ')'
	;

VersionedQualifiedReferenceName returns ecore::EString:
	(VersionedQualifiedName '::')? ID ('::' ID)*;

MInterface returns MInterface:
	'interface'
	domain=[mclevdom::MDomain|VersionedQualifiedName]
	'::'
	type=[mclevdom::MInterfaceType|VersionedQualifiedReferenceName]
	name=ID
	('extends' extends+=[MInterface|VersionedQualifiedName] ( "," extends+=[MInterface|VersionedQualifiedName])* )?
	'{'
		'version' ':=' version=Version ';'
    '}'
    ';';
